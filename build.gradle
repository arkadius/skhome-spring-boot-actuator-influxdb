buildscript {
    ext {
        javaVersion = 1.8
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'
apply plugin: 'propdeps-eclipse'

sourceCompatibility = javaVersion
targetCompatibility = javaVersion

compileJava.options*.compilerArgs = [
        "-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
        "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
        "-Xlint:overrides", "-Xlint:path", "-Xlint:static", "-Xlint:try",
        "-Xlint:fallthrough", "-Xlint:rawtypes", "-Xlint:deprecation",
        "-Xlint:unchecked", "-Xlint:-options", "-Werror"
]

compileTestJava.options*.compilerArgs = [
        "-Xlint:serial", "-Xlint:varargs", "-Xlint:cast", "-Xlint:classfile",
        "-Xlint:dep-ann", "-Xlint:divzero", "-Xlint:empty", "-Xlint:finally",
        "-Xlint:overrides", "-Xlint:path", "-Xlint:static", "-Xlint:try",
        "-Xlint:-fallthrough", "-Xlint:-rawtypes", "-Xlint:-deprecation",
        "-Xlint:unchecked", "-Xlint:-options", "-Xlint:deprecation"
]

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
    javadocLinks = [
            'http://docs.oracle.com/javase/7/docs/api/',
            'http://docs.oracle.com/javaee/6/api/',
            'http://docs.spring.io/spring/docs/current/javadoc-api/'
    ] as String[]
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
    compile group: 'org.influxdb', name: 'influxdb-java', version: '2.7'
    provided group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    optional group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: springBootVersion

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.4.1'
}

group 'net.skhome'
version '1.0-SNAPSHOT'

compileJava.dependsOn(processResources)

test {
    systemProperty("java.awt.headless", "true")
    logging.captureStandardOutput LogLevel.INFO
}

jar {
    manifest.attributes['Created-By'] = "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
    manifest.attributes["Implementation-Title"] = project.name
    manifest.attributes["Implementation-Version"] = project.version
}

javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = project.name
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO
}

task sourcesJar(type: Jar, dependsOn: classes) {
    group = 'build'
    description = 'Assembles a jar archive containing the main sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    group = 'build'
    description = 'Assembles a jar archive containing the JavaDoc API documentation.'
    classifier = "javadoc"
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
